ext {
    winswVersion = '2.0.2'
    winswBuildDir = "${project.buildDir}/windows-service/"
    winswLanguage = 'zh_CN'
    winswProfile = project.hasProperty("profile") ? profile : "prod"
}

repositories {
    maven{
        url 'http://repo.jenkins-ci.org/releases/'
    }
}

configurations{
    winsw { transitive = false }
}

dependencies {
    //WinSW
    winsw "com.sun.winsw:winsw:${winswVersion}:bin@exe"
}

task winswRepackage(dependsOn: bootRepackage, type: Copy) {
    def outPath = project.hasProperty("winswProfile") ?
            "${winswBuildDir}/${winswProfile}" :
            "${winswBuildDir}"

    from configurations.winsw
    from "${project.buildDir}/libs/"
    from "${rootProject.rootDir}/gradle/batch/winsw_${winswLanguage}.bat"
    into "${outPath}"
    rename "winsw-${winswVersion}-bin.exe", "${project.name}.exe"
    rename "${project.name}-${version}.jar", "${project.name}.jar"
    rename "winsw_${winswLanguage}.bat", "${project.name}.bat"

    doFirst{
        new File("${outPath}").mkdir()
    }

    doLast{
        def configuration = {
            mkp.xmlDeclaration()
            configuration() {
                id("${project.name}")
                name("${project.name}")
                executable("java")
                description([:], "${project.description}-${version}")
                arguments(
                        (project.hasProperty("winswProfile") ?
                                "-Dspring.profiles.active=${winswProfile} " :
                                "") +
                        "-jar \"${project.name}.jar\"")
                logmode("rotate")
            }
        }

        def builder = new groovy.xml.StreamingMarkupBuilder()
        builder.encoding = "UTF-8"
        def xml = new File("${outPath}/${project.name}.xml")
        xml.write(groovy.xml.XmlUtil.serialize(builder.bind(configuration)))
    }
}

configure(winswRepackage) {
    group = 'winsw'
    description = 'Spring Boot winsw build'
}
